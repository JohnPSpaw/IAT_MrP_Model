###John Spaw
###Multi-level regression (binary) with post-stratification (state level)
###Model definition for fit in JAGS Gibbs sampler

#what is this
data {
  for (i in 1:num_state) {
    census_sum[i] <- sum(census_count[,i])
  }
}

model {
  #individual level responses
  #i indexes individuals from  survey
  for (i in 1:num_y) {
    y[i] ~ dbern(p[i])
    logit(p[i]) <- inprod(x[i,], beta[,state[i]])
  }
  
  #set priors on linear predictors for each state
  for (i in 1:num_state) {
    beta[1:num_x, i] ~ dmnorm(gamma %*% state_x[,i], tau)
  }
  
  #Linear priors
  #priors for mean of linear coefficients
  for (i in 1:num_x) {
    gamma[i,1:num_state_x] ~ dmnorm(gamma_mean, gamma_prec)
  }
  #prior for precision (reciprocal of variance) of linear coefficients
  tau ~ dwish(tau_R, tau_k)
  
  #State level predictions
  for (i in 1:num_state) {
    for (j in 1:num_cells) {
      logit(p_cell[j,i]) <- inprod(pred_x[j,], beta[,i])
    }
    predict[i] <- inprod(p_cell[,i], census_count[,i]) / census_sum[i]
  }
}
